notedown_document = { 
	paragraph_block | 
	blockquote_block | 
	centered_blockquote_block |
	ordered_list_block |
	unordered_list_block |
	horizontal_line |
	linked_text |
	modified_text |
	modifier |
	hint_line |
	heading_line
};



paragraph_block = paragraph_line, { paragraph_line };
blockquote_block = blockquote_line, { blockquote_line };
centered_blockquote_block = centered_blockquote_line, { centered_blockquote_line };
ordered_list_block = ordered_list_line, { ordered_list_line };
unordered_list_block = unordered_list_line, { unordered_list_line };
horizontal_line = '-', '-', '-', { '-' | space }, '\n';

linked_text = '[', any_text, ']', [ '?' | '!' ], '(', url, [ text_settings ], ')';
modified_text = '[', any_text, ']', modifier;
modifier = '{', text_settings, '}';



blockquote_line = '>', space, any_line;
paragraph_line = notedown_line;
heading_line = heading_indicator, space, any_text;
centered_blockquote_line = '>', '>', space, any_line;

hint_line = '>', hint_tag, '>', space, any_line;
hint_tag = 
	"info" | 'i' | ":i:" | ":info:" |
	"warn" | 'w' | ":w:" | ":warn:" |
	"error" | 'e'| ":e:" | ":error:"
;

text_settings = {
	title | 
	id |
	class |
	attribute |
	function |
	style
};

title = ':', escaped_arg;
id = '#', text;
class = '.', text, [ class ];
attribute = '+', text, '=', escaped_arg;
function = '$', text, { funcargs };
funcargs = ' ', escaped_arg;
style = '>', text, ':', escaped_arg;
escaped_arg = text | ( '"', text, { text | space }, '"' );

ordered_list_line = { space }, digit, '.', space, any_line;
unordered_list_line = { space }, '-', space, any_line;

notedown_line = { any_text | space }, [ forced_linebreak ], '\n';

any_line = blockquote_line | paragraph_line | heading_line;

any_text =  text_elements, { text_elements | space };
text_elements = text | style_text | emoji;
text = { letter | escape_sequence };
digit = '0'..'9';
letter = 'a'..'z' | 'A'..'Z' | digit | '.' | ',' | ';' | ...;
space = ' ';
escape_sequence = '\n' | '\\', '\*', '\#', '\_', '\/', '\~', '\=', '\`', '\:', '\-';
heading_indicator = '#', [ '#' ], [ '#' ], [ '#' ], [ '#' ], [ '#' ];
forced_linebreak = space, space;

style_text = 
	bold_text |
	italic_text |
	underline_text |
	strikethrough_text |
	highlight_text |
	inline_code_text
;

bold_text = '*', any_text, '*';
italic_text = '/', any_text, '/';
underline_text = '_', any_text, '_';
strikethrough_text = '~', any_text, '~';
highlight_text = '=', any_text, '=';
inline_code_text = '`', any_text, '`';

emoji = ':', text, { text, '_' }, ':';